title: Start
tags:
---
Player: Another day in Space Fleet. Might go have a chat... #line:08b260d 
//pick a random person to speak to
-> Go see the Engineer as per orders #line:0fcaf5d 
    <<jump TalkToEngineer>>
-> Meet up with your friend #line:072c935 
    <<jump TalkToCrewmate>>
-> Go and talk to the Captain #line:084570b 
    <<jump TalkToCaptain>>
===

title: TalkToEngineer
tags:
---
Engineer: Hello! I am the Engineer. #line:064dd09 
<<jump BridgeEnding>>
===

title: TalkToCrewmate
tags:
---
Crewmate: Hello! I am your Crewmate. #line:0870ea4 
<<jump BridgeEnding>>
===

title: TalkToCaptain
tags:
---
<<declare $away_mission_readiness=0>>


Captain: Hello! I am the Captain. #line:0861e59 
Player: I want to go on more missions. #line:008670d 
Captain: Do you think you are ready? #line:05198bc 
-> Yes #line:014e949 
    <<set $away_mission_readiness+=1>>
    //if so, ask again
    Captain: Really? #line:026c113 
    -> ...yes? #line:087956f 
        <<set $away_mission_readiness+=1>>
    -> Actually, no. #line:00617df 
-> No #line:0c3962d 
//now go to the ending
<<jump BridgeEnding>>
===

title: BridgeEnding
tags:
---
//everyone reports to the bridge
Captain: Pirates! #line:0a59293 
Player: Oh no! #line:0486e25 
//now change who goes depending on player actions
<<if $away_mission_readiness<2>>
    Captain: Crewmate, go deal with those pirates! #line:0c2860e 
    Crewmate: Yes, Captain! #line:0ed6828 
<<else>>
    Captain: Player, you were just telling me how ready for this you are. Go deal with those pirates! #line:07c3c84 
    Player: Hooray! #line:08fb9fb 
<<endif>>
===






title: RealStart
tags:
---
Player: At last...
Player: I've finally arrived at the next town.
Player: It sure seems busy, but it looks like the road to the next town is closed.
Player: Maybe I should talk to someone in the town square and find out what's going on?
===

title: NPC1Information
tags:
---
<<declare $visited_town_crier = 0>>
<<declare $rejected_town_crier = 0>>

<<if $rejected_town_crier < 1>>
Town Crier: Welcome to Seaswan! Is there something I can help you with?
    -> Yes, how can I move to the next town?
        Town Crier: The three head villagers are looking for someone to help them out.
        Town Crier: They won't let anyone leave until their requests are met!
        Town Crier: Maybe you can help them?
            -> Yes
                <<set $visited_town_crier = 1>>
                Town Crier: Great! Hop to it.
            -> No
                <<set $rejected_town_crier = 1>>
                Town Crier: Well, you're stuck here with the rest of us until someone actually useful comes along...
    -> No, I'm good.
<<else>>
Town Crier: Welcome back...
Town Crier: Are you finally ready to help out yet?
    -> Fine, but it's not as if I want to.
        <<set $visited_town_crier = 1>>
        Town Crier: Get to work then.
    -> As if.
        Town Crier: Then continue to enjoy waiting along with the rest of us.
<<endif>>
===

title: NPC2Baker
tags:
---
<<declare $rejected_baker = 0>>

<<if $visited_town_crier<1>>
    <<jump NotVisitedCrierYet>>
<<endif>>

<<if $rejected_baker < 1>>
Baker: At last! A new face in town.
Baker: You've come just in time, I need some butter for the hot bread that has just come out of the oven.
Baker: Would you kindly go and fetch it?
Baker: If you don't make it back in five minutes, my bread will be cold and I will be very sad.
Baker: How about it?
    -> Yes, I'll run and fetch it.
        Thank you so much! The five minutes starts now.
    -> No way, why should I make you your lunch?
        <<set $rejected_baker = 1>>
        Baker: Well that's just rude.
<<endif>>

<<if $rejected_baker > 0>>
Baker: So you decided to come back?
Baker: Luckily for you, I just finished making another fresh loaf of bread.
Baker: Are you ready to get the butter?
    -> Yes, fine. But only this once!
        Baker: I had a feeling you would see reason. Your five minutes starts now.
    -> Not a chance. Make your own sandwich.
        Baker: Good luck moving on to the next town without my help then.
<<endif>>
===

title: NPC3Escort
tags:
---
<<if $visited_town_crier<1>>
    <<jump NotVisitedCrierYet>>
<<endif>>

Villager: Hello.
===

title: NPC4Delivery
tags:
---
<<if $visited_town_crier<1>>
    <<jump NotVisitedCrierYet>>
<<endif>>

Merchant: Hello.
===

title: NotVisitedCrierYet
tags:
---
Player: I should visit the Town Crier before speaking to anyone else.
===

