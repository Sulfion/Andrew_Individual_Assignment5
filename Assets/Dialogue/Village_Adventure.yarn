title: Start
tags:
---
Player: Another day in Space Fleet. Might go have a chat... #line:08b260d 
//pick a random person to speak to
-> Go see the Engineer as per orders #line:0fcaf5d 
    <<jump TalkToEngineer>>
-> Meet up with your friend #line:072c935 
    <<jump TalkToCrewmate>>
-> Go and talk to the Captain #line:084570b 
    <<jump TalkToCaptain>>
===

title: TalkToEngineer
tags:
---
Engineer: Hello! I am the Engineer. #line:064dd09 
<<jump BridgeEnding>>
===

title: TalkToCrewmate
tags:
---
Crewmate: Hello! I am your Crewmate. #line:0870ea4 
<<jump BridgeEnding>>
===

title: TalkToCaptain
tags:
---
<<declare $away_mission_readiness=0>>


Captain: Hello! I am the Captain. #line:0861e59 
Player: I want to go on more missions. #line:008670d 
Captain: Do you think you are ready? #line:05198bc 
-> Yes #line:014e949 
    <<set $away_mission_readiness+=1>>
    //if so, ask again
    Captain: Really? #line:026c113 
    -> ...yes? #line:087956f 
        <<set $away_mission_readiness+=1>>
    -> Actually, no. #line:00617df 
-> No #line:0c3962d 
//now go to the ending
<<jump BridgeEnding>>
===

title: BridgeEnding
tags:
---
//everyone reports to the bridge
Captain: Pirates! #line:0a59293 
Player: Oh no! #line:0486e25 
//now change who goes depending on player actions
<<if $away_mission_readiness<2>>
    Captain: Crewmate, go deal with those pirates! #line:0c2860e 
    Crewmate: Yes, Captain! #line:0ed6828 
<<else>>
    Captain: Player, you were just telling me how ready for this you are. Go deal with those pirates! #line:07c3c84 
    Player: Hooray! #line:08fb9fb 
<<endif>>
===






title: RealStart
tags:
---
Player: At last... #line:07747c1 
Player: I've finally arrived at the next town. #line:02e6f78 
Player: It sure seems busy, but it looks like the road to the next town is closed. #line:06a4814 
Player: Maybe I should talk to someone in the town square and find out what's going on? #line:0955e4a 
===

title: NPC1Information
tags:
---
<<declare $visited_town_crier=0>>
<<declare $rejected_town_crier=0>>

<<if $visited_town_crier==1>>
Town Crier: As soon as you finish your tasks, you'll be able to leave. #line:0721654 
Town Crier: Good luck! #line:0078aec 
<<endif>>

<<if $rejected_town_crier==1&&$visited_town_crier==0>>
Town Crier: Welcome back... #line:0801a07 
Town Crier: Are you finally ready to help out yet? #line:0e0f81d 
    -> Fine, but it's not as if I want to. #line:0e1057c 
        <<set $visited_town_crier=1>>
        Town Crier: Get to work then. #line:0e6ea68 
    -> As if. #line:0fa6239 
        Town Crier: Then continue to enjoy waiting along with the rest of us. #line:0fca3ec 
<<endif>>

<<if $rejected_town_crier==0&&$visited_town_crier==0>>
Town Crier: Welcome to Seaswan! Is there something I can help you with? #line:0c550ad 
    -> Yes, how can I move to the next town? #line:0694c66 
        Town Crier: The three head villagers are looking for someone to help them out. #line:0056c80 
        Town Crier: They won't let anyone leave until their requests are met! #line:0b722a5 
        Town Crier: Maybe you can help them? #line:02c2c25 
            -> Sure, I need to if I want to move on. #line:04d5416 
                <<set $visited_town_crier=1>>
                Town Crier: Great! Hop to it. #line:0d84619 
            -> No, this is supposed to be my vacation! #line:0142d21 
                <<set $rejected_town_crier=1>>
                Town Crier: Well, you're stuck here with the rest of us until someone actually useful comes along... #line:09883a4 
    -> No, I'm good. #line:0820785 
        Town Crier: Ok, if you need anything you know where to find me. #line:01714e0 
<<endif>>
===

title: NPC2Baker
tags:
---
<<declare $rejected_baker=0>>
<<declare $accepted_baker=0>>
<<declare $baker_quest_complete_tracker=0>>

<<if $visited_town_crier==0>>
    <<jump NotVisitedCrierYet>>
<<endif>>

<<if $rejected_baker==1&&$accepted_baker==0>>
Baker: So you decided to come back? #line:0eeb48e 
Baker: Luckily for you, I just finished making another fresh loaf of bread. #line:0bd5644 
Baker: Are you ready to get the butter? #line:0f4615d 
    -> Yes, fine. But only this once! #line:0376e5c 
        <<set $accepted_baker=1>>
        Baker: I had a feeling you would see reason. Your five minutes starts now. #line:0ab491e 
    -> Not a chance. Make your own sandwich. #line:0223d11 
        Baker: Good luck moving on to the next town without my help then. #line:0c099c5 
<<endif>>

<<if $rejected_baker==0>>
Baker: At last! A new face in town. #line:055b31e 
Baker: You've come just in time, I need some butter for the hot bread that has just come out of the oven. #line:0909ddb 
Baker: Would you kindly go and fetch it? It's on a purple table. #line:00a7e8a 
Baker: If you don't make it back in five minutes, my bread will be cold and I will be very sad. #line:0a74f73 
Baker: How about it? #line:09e4500 
    -> Yes, I'll run and fetch it. #line:0b997ec 
        <<set $accepted_baker=1>>
        Thank you so much! The five minutes starts now. #line:0a72ec2 
    -> No way, why should I make you your lunch? #line:0ab821a 
        <<set $rejected_baker=1>>
        Baker: Well that's just rude. #line:0735798 
<<endif>>
===

title: NPC3Escort
tags:
---
<<declare $rejected_escort=0>>
<<declare $accepted_escort=0>>
<<declare $escort_quest_complete_tracker=0>>

<<if $visited_town_crier==0>>
    <<jump NotVisitedCrierYet>>
<<endif>>

<<if $rejected_escort==1&&$accepted_escort==0>>
Villager: Did you have a change of heart? It'll only take a minute to look at the flowers. #line:0c0602d 
    -> Yes I found some free time. #line:00dd1e4 
        <<set $accepted_escort=1>>
        Villager: Thank you! Let's head out. #line:08e5267 
    -> Nope, just on my way to somewhere else. #line:02a4183 
        Villager: I guess I'll never be able to see the flowers again... #line:0bd0e37 
<<endif>>

<<if $rejected_escort==0&&$accepted_escort==0>>
Villager: Hi there! #line:0a5bc5b 
Villager: I love flowers, but I'm too scared to go look at them alone. #line:048f598 
Villager: Would you kindly come with me to look at them? #line:099e60b 
Villager: I'll be very scared and run home if you go too far away from me! #line:09d11d6 
    -> Sure, I have some free time. #line:085b507 
        <<set $accepted_escort=1>>
        Villager: Yay, let's go have a look! #line:05f3481 
    -> No I'm in a bit of a rush and can't stop to smell the roses. #line:041953e 
        <<set $rejected_escort=1>>
        Villager: You won't even take a minute out of your day to help someone... #line:031b002 
<<endif>>
===

title: NPC4Delivery
tags:
---
<<declare $rejected_delivery=0>>
<<declare $accepted_delivery=0>>
<<declare $delivery_quest_complete_tracker=0>>

<<if $visited_town_crier==0>>
    <<jump NotVisitedCrierYet>>
<<endif>>

<<if $rejected_delivery==1&&$accepted_delivery==0>>
Merchant: Come crawling back have you? #line:0ebe974 
Merchant: If you want to get through that gate, you'd better help me out. #line:09abefb 
Merchant: Unless you're happy to just loaf about here longer. #line:0c79a08 
    -> Fine... I guess I don't have a choice. #line:0f5403b 
        <<set $accepted_delivery=1>>
        Merchant: The items are on the table. Get to it. #line:0175a98 
    -> I choose to loaf. #line:07b9385 
        Merchant: Then loaf you shall. #line:0d80806 
<<endif>>

<<if $rejected_delivery==0&&$accepted_delivery==0>>
Merchant: You're just in time! #line:07db4f7 
Merchant: I have a delivery for the other two head villagers. #line:0d01c8f 
Merchant: Can you please bring them each the correct item from the black table? #line:0d4d0e9 
    -> Not a problem. #line:0bebb0b 
        <<set $accepted_delivery=1>>
        Merchant: Thank you so much, good luck! #line:0206bb7 
    -> Do I look like a deliveryman? #line:091374a 
        <<set $rejected_delivery=1>>
        Merchant: If you're going to give me attitude then I don't need nor want your help anyways. #line:069c45e 
<<endif>>
===

title: NotVisitedCrierYet
tags:
---
Player: I should visit the Town Crier before speaking to anyone else. #line:0f22561 
===

