title: Start
tags:
---
Player: Another day in Space Fleet. Might go have a chat... #line:08b260d 
//pick a random person to speak to
-> Go see the Engineer as per orders #line:0fcaf5d 
    <<jump TalkToEngineer>>
-> Meet up with your friend #line:072c935 
    <<jump TalkToCrewmate>>
-> Go and talk to the Captain #line:084570b 
    <<jump TalkToCaptain>>
===

title: TalkToEngineer
tags:
---
Engineer: Hello! I am the Engineer. #line:064dd09 
<<jump BridgeEnding>>
===

title: TalkToCrewmate
tags:
---
Crewmate: Hello! I am your Crewmate. #line:0870ea4 
<<jump BridgeEnding>>
===

title: TalkToCaptain
tags:
---
<<declare $away_mission_readiness=0>>


Captain: Hello! I am the Captain. #line:0861e59 
Player: I want to go on more missions. #line:008670d 
Captain: Do you think you are ready? #line:05198bc 
-> Yes #line:014e949 
    <<set $away_mission_readiness+=1>>
    //if so, ask again
    Captain: Really? #line:026c113 
    -> ...yes? #line:087956f 
        <<set $away_mission_readiness+=1>>
    -> Actually, no. #line:00617df 
-> No #line:0c3962d 
//now go to the ending
<<jump BridgeEnding>>
===

title: BridgeEnding
tags:
---
//everyone reports to the bridge
Captain: Pirates! #line:0a59293 
Player: Oh no! #line:0486e25 
//now change who goes depending on player actions
<<if $away_mission_readiness<2>>
    Captain: Crewmate, go deal with those pirates! #line:0c2860e 
    Crewmate: Yes, Captain! #line:0ed6828 
<<else>>
    Captain: Player, you were just telling me how ready for this you are. Go deal with those pirates! #line:07c3c84 
    Player: Hooray! #line:08fb9fb 
<<endif>>
===






title: RealStart
tags:
---
Player: At last...
Player: I've finally arrived at the next town.
Player: It sure seems busy, but it looks like the road to the next town is closed.
Player: Maybe I should talk to someone in the town square and find out what's going on?
===

title: NPC1Information
tags:
---
<<declare $visited_town_crier = 0>>
<<declare $rejected_town_crier = 0>>

<<if $visited_town_crier == 1>>
Town Crier: As soon as you finish your tasks, you'll be able to leave.
Town Crier: Good luck!
<<endif>>

<<if $rejected_town_crier == 1>>
Town Crier: Welcome back...
Town Crier: Are you finally ready to help out yet?
    -> Fine, but it's not as if I want to.
        <<set $visited_town_crier = 1>>
        Town Crier: Get to work then.
    -> As if.
        Town Crier: Then continue to enjoy waiting along with the rest of us.
<<endif>>

<<if $rejected_town_crier == 0 && $visited_town_crier == 0>>
Town Crier: Welcome to Seaswan! Is there something I can help you with?
    -> Yes, how can I move to the next town?
        Town Crier: The three head villagers are looking for someone to help them out.
        Town Crier: They won't let anyone leave until their requests are met!
        Town Crier: Maybe you can help them?
            -> Sure, I need to if I want to move on.
                <<set $visited_town_crier = 1>>
                Town Crier: Great! Hop to it.
            -> No, this is supposed to be my vacation!
                <<set $rejected_town_crier = 1>>
                Town Crier: Well, you're stuck here with the rest of us until someone actually useful comes along...
    -> No, I'm good.
        Town Crier: Ok, if you need anything you know where to find me.
<<endif>>
===

title: NPC2Baker
tags:
---
<<declare $rejected_baker = 0>>
<<declare $accepted_baker = 0>>
<<declare $baker_quest_complete_tracker to 0>>

<<if $visited_town_crier == 0>>
    <<jump NotVisitedCrierYet>>
<<endif>>

<<if $rejected_baker == 1 && $accepted_baker == 0>>
Baker: So you decided to come back?
Baker: Luckily for you, I just finished making another fresh loaf of bread.
Baker: Are you ready to get the butter?
    -> Yes, fine. But only this once!
        <<set $accepted_baker = 1>>
        Baker: I had a feeling you would see reason. Your five minutes starts now.
    -> Not a chance. Make your own sandwich.
        Baker: Good luck moving on to the next town without my help then.
<<endif>>

<<if $rejected_baker == 0>>
Baker: At last! A new face in town.
Baker: You've come just in time, I need some butter for the hot bread that has just come out of the oven.
Baker: Would you kindly go and fetch it?
Baker: If you don't make it back in five minutes, my bread will be cold and I will be very sad.
Baker: How about it?
    -> Yes, I'll run and fetch it.
        <<set $accepted_baker = 1>>
        Thank you so much! The five minutes starts now.
    -> No way, why should I make you your lunch?
        <<set $rejected_baker = 1>>
        Baker: Well that's just rude.
<<endif>>
===

title: NPC3Escort
tags:
---
<<declare $rejected_escort = 0>>
<<declare $accepted_escort = 0>>
<<declare $escort_quest_complete_tracker to 0>>

<<if $visited_town_crier == 0>>
    <<jump NotVisitedCrierYet>>
<<endif>>

<<if $rejected_escort == 1 && $accepted_escort == 0>>
Villager: Did you have a change of heart? It'll only take a minute to look at the flowers.
    -> Yes I found some free time.
        <<set $accepted_escort = 1>>
        Villager: Thank you! Let's head out.
    -> Nope, just on my way to somewhere else.
        Villager: I guess I'll never be able to see the flowers again...
<<endif>>

<<if $rejected_escort == 0 && $accepted_escort == 0>>
Villager: Hi there!
Villager: I love flowers, but I'm too scared to go look at them alone.
Villager: Would you kindly come with me to look at them?
Villager: I'll be very scared and run home if you go too far away from me!
    -> Sure, I have some free time.
        <<set $accepted_escort = 1>>
        Villager: Yay, let's go have a look!
    -> No I'm in a bit of a rush and can't stop to smell the roses.
        <<set $rejected_escort = 1>>
        Villager: You won't even take a minute out of your day to help someone...
<<endif>>
===

title: NPC4Delivery
tags:
---
<<declare $rejected_delivery = 0>>
<<declare $accepted_delivery = 0>>
<<declare $delivery_quest_complete_tracker to 0>>

<<if $visited_town_crier == 0>>
    <<jump NotVisitedCrierYet>>
<<endif>>

<<if $rejected_delivery == 1 && $accepted_delivery == 0>>
Merchant: Come crawling back have you?
Merchant: If you want to get through that gate, you'd better help me out.
Merchant: Unless you're happy to just loaf about here longer.
    -> Fine... I guess I don't have a choice.
        <<set $accepted_delivery = 1>>
        Merchant: The items are on the table. Get to it.
    -> I choose to loaf.
        Merchant: Then loaf you shall.
<<endif>>

<<if $rejected_delivery == 0 && $accepted_delivery == 0>>
Merchant: You're just in time!
Merchant: I have a delivery for the other two head villagers.
Merchant: Can you please bring them each the correct item?
    -> Not a problem.
        <<set $accepted_delivery = 1>>
        Merchant: Thank you so much, good luck!
    -> Do I look like a deliveryman?
        <<set $rejected_delivery = 1>>
        Merchant: If you're going to give me attitude then I don't need nor want your help anyways.
<<endif>>
===

title: NotVisitedCrierYet
tags:
---
Player: I should visit the Town Crier before speaking to anyone else.
===

